repositories {
    mavenCentral()
}

configure(allprojects) {
    version = '1.0'
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.google.guava:guava:19.0'
        compile 'org.slf4j:slf4j-api:1.7.12'

        testCompile ('junit:junit:4.12') {
            exclude group: 'org.hamcrest'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.mockito:mockito-all:1.10.19'
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }

        test {
            java {
                srcDir 'unit-test'
                srcDir 'integration-test'
            }
        }
    }
}

project(':application') {
    dependencies {
        compile project(':domain')
        testCompile project(':domain').sourceSets.test.output
    }
}

project(':persistence') {
    dependencies {
        compile project(':domain')
        testCompile project(':domain').sourceSets.test.output
    }
}

project(':services') {
    dependencies {
        compile project(':domain')
        compile project(':application')
        testCompile project(':domain').sourceSets.test.output
    }
}

project(':packaging') {
    dependencies {
        compile project(':domain')
        compile project(':application')
        compile project(':persistence')
        compile project(':services')
    }
}

task copyJarOutput(type: Copy, dependsOn: ':packaging:shoppingListJar') {
    from tasks.getByPath(':packaging:shoppingListJar').outputs.files
    rename 'packaging-' + version + '-standalone.jar', 'shopping-list.jar'
    into { 'build' }
}

task build(dependsOn: copyJarOutput)
